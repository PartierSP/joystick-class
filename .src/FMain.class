' Gambas class file

'Declare a Joystick so it can be dynamically created later in the Form_Open subroutine
Public Joystk3 As Joystick

Public Sub Form_Open()

  Me.Center()
  
  'Check if Joystk1 is ready.
  If Joystk0.Ready = False Then 
    Message.Warning("Joystick 0 not found.")
    'Enable any kind of fall back routines here (either prompt user to check hardware and settings and/or enable mouse/keyboard input instead)
  Endif

  'Create another joystick instance for device js3
  'We are not expecting this joystick to connect, but it demonstrates
  'how to dynamically create a joystick connection at runtime.
  Joystk3 = New Joystick(Me, "/dev/input/js3")

  'Check if Joystk3 is ready.
  If Joystk3.Ready = False Then 
    Message.Warning("Joystick 3 not found.")
  Endif

End

Public Sub Form_Close()

  'Must close off all open Joysticks before exiting program.
  Joystk0.Close
  Joystk3.Close

End

Public Sub Joystk0_AxisChange(Id As Integer, Value As Integer)

  'A movement occured on one of the joystick axises.
  'Id = which axis we are dealing with.
  'Value = the new reading for the identified axis (maximum range of -32767 to 32767).
  'NOTE: some joysticks may not use the full range due to some physical/hardware limitation.
  Select Id
    Case 0
      pbAxis0.Value = ScaleAxis(Value)
    Case 1
      pbAxis1.Value = ScaleAxis(Value)
    Case 2
      pbAxis2.Value = ScaleAxis(Value)
    Case 3
      pbAxis3.Value = ScaleAxis(Value)
    Case 4
      pbAxis4.Value = ScaleAxis(Value)
    Case 5
      pbAxis5.Value = ScaleAxis(Value)
  End Select

End

Public Sub Joystk0_ButtonChange(Id As Integer, Value As Boolean)

  'A button state has changed.
  'Id = which button was changed.
  'Value = the button's current state.  1=on, 0=off
  Select Id
    Case 0
      cbButton0.Value = Value
    Case 1
      cbButton1.Value = Value
    Case 2
      cbButton2.Value = Value
    Case 3
      cbButton3.Value = Value
    Case 4
      cbButton4.Value = Value
    Case 5
      cbButton5.Value = Value
    Case 6
      cbButton6.Value = Value
    Case 7
      cbButton7.Value = Value
    Case 8
      cbButton8.Value = Value
    Case 9
      cbButton9.Value = Value
    Case 10
      cbButton10.Value = Value
    Case 11
      cbButton11.Value = Value
    Case 12
      cbButton12.Value = Value
  End Select

End

Public Sub ScaleAxis(Value As Integer) As Float

  'Progress bars require values of 0 to 1 while
  'joystick axises vales are from -32767 to 32767.
  'So these need to be scaled prior to using on a
  'progress bar.

  Dim fOutput As Float

  'Axis max travel is -32767 to 32767
  fOutput = (Value + 32767) / 65534

  Return fOutput

End
